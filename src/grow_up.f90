!======================================================================!
subroutine grow_up
!----------------------------------------------------------------------!
use params
use vars
!----------------------------------------------------------------------!
implicit none
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
! Initial total C in individual                            (kg[C] ind-1)
!----------------------------------------------------------------------!
C0 = CDM * (Mfol + Ma + Mb + Mfro) + fCsucrose * Su + fCStarch * St
!----------------------------------------------------------------------!
! Save initial quantities.
!----------------------------------------------------------------------!
rb0 = rb
Su0 = Su
St0 = St
Mfol0 = Mfol
Ma0 = Ma
Mb0 = Mb
Mfro0 = Mfro
Mtot0 = Mfol + Ma + Mb + Mfro
!----------------------------------------------------------------------!
! Basal C balance decays towards 0 ()
!----------------------------------------------------------------------!
An_base = An_base - dt * An_base / tau_Anbase
!----------------------------------------------------------------------!
! Percentage loss of conductivity decays towards 0                   (%)
!----------------------------------------------------------------------!
PLC = PLC - dt * PLC / tau_PLC
!----------------------------------------------------------------------!
! Gross photosynthesis                           (kg[C] m[ground]-2 s-1)
!----------------------------------------------------------------------!
Ag_crown_C = 1.0e-3 * MC * Ag_crown_mol
!----------------------------------------------------------------------!
! Net photosynthesis                             (kg[C] m[ground]-2 s-1)
!----------------------------------------------------------------------!
An_crown_C = 1.0e-3 * MC * An_crown_mol
!----------------------------------------------------------------------!
! Foliage respiration                            (kg[C] m[ground]-2 s-1)
!----------------------------------------------------------------------!
Rd_crown_C = Ag_crown_C - An_crown_C
!----------------------------------------------------------------------!
! Gross C uptake by crown                              (kg[C] ind-1 s-1)
!----------------------------------------------------------------------!
Cup = CPA * Ag_crown_C
!----------------------------------------------------------------------!
! Crown height effect on basal radial growth (fraction)
!----------------------------------------------------------------------!
fIAA = one - ht / 100.0
fIAA = max (zero, fIAA)
fIAA = min (one , fIAA)
!----------------------------------------------------------------------!
! Rate of change of basal radius                                 (m s-1)
!----------------------------------------------------------------------!
drb = fW_gr * fT_gr * fIAA * fa_Su * drb_base
!----------------------------------------------------------------------!
! Rate of change of rooting depth                                (m s-1)
!----------------------------------------------------------------------!
ddp = fW_gr * fT_gr *        fa_Su * ddp_base
!----------------------------------------------------------------------!
! Rate of change of height                                       (m s-1)
!----------------------------------------------------------------------!
dht = fW_gr * fT_gr * fIAA * fa_Su * dht_base
!----------------------------------------------------------------------!
! Basal heartwood area                                              (m2)
!----------------------------------------------------------------------!
Ahw = pi * rb_hw ** 2
!----------------------------------------------------------------------!
! Basal stem area                                                   (m2)
!----------------------------------------------------------------------!
Astem = pi * rb0 ** 2
!----------------------------------------------------------------------!
! Basal sapwood area                                                (m2)
!----------------------------------------------------------------------!
Asw = Astem - Ahw
!----------------------------------------------------------------------!
! Potential amount of additional foliage mass             (kg[DM] ind-1)
!----------------------------------------------------------------------!
GMfol_lim = lasa * Asw / sla - Mfol0
!----------------------------------------------------------------------!
! If sapwood area allows, grow foliage                      (kg[DM] s-1)
!----------------------------------------------------------------------!
if (GMfol_lim > zero) then
  GMfol = fW_gr * fT_gr * dMfol_base * Asw
  GMfol = min (GMfol, (GMfol_lim - Mfol0) / dt)
else
  GMfol = zero
end if
!----------------------------------------------------------------------!
! Foliage litter flux                                 (kg[DM] ind-1 s-1)
!----------------------------------------------------------------------!
LMfol = Mfol0 / tau_fol
!----------------------------------------------------------------------!
! Rate of change of foliage mass                      (kg[DM] ind-1 s-1)
!----------------------------------------------------------------------!
dMfol = GMfol - LMfol
!----------------------------------------------------------------------!
! Increment foliage mass                                  (kg[DM] ind-1)
!----------------------------------------------------------------------!
Mfol = Mfol + dt * dMfol
!----------------------------------------------------------------------!
! Increment height                                                   (m)
!----------------------------------------------------------------------!
ht = ht + dt * dht
!----------------------------------------------------------------------!
! Increment basal radius                                             (m)
!----------------------------------------------------------------------!
rb = rb + dt * drb
!----------------------------------------------------------------------!
!write (*,*) float (syr) * Cup / CPA, float (syr) * drb, &
!            float (syr) * ddp, float (syr) * dht
!----------------------------------------------------------------------!

!----------------------------------------------------------------------!
end subroutine grow_up
!======================================================================!
